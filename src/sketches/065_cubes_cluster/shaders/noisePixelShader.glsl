uniform sampler2D normal;
uniform float iTime;
uniform float phaseFactor;
varying vec2 vUV;

#define PI 3.141592653589
#define E 2.718281828459
#define EPSILON 0.0000001

#define MOD3 vec3(.1031, .11369, .13787)

vec3 hash33(vec3 p3) {
  p3 = fract(p3 * MOD3);
  p3 += dot(p3, p3.yxz + 19.19);
  return -1.0 + 2.0 * fract(vec3((p3.x + p3.y) * p3.z, (p3.x + p3.z) * p3.y, (p3.y + p3.z) * p3.x));
}

float noise(vec3 p) {
  const float K1 = 0.333333333;
  const float K2 = 0.166666667;

  vec3 i = floor(p + (p.x + p.y + p.z) * K1);
  vec3 d0 = p - (i - (i.x + i.y + i.z) * K2);

  vec3 e = step(vec3(0.0), d0 - d0.yzx);
  vec3 i1 = e * (1.0 - e.zxy);
  vec3 i2 = 1.0 - e.zxy * (1.0 - e);

  vec3 d1 = d0 - (i1 - 1.0 * K2);
  vec3 d2 = d0 - (i2 - 2.0 * K2);
  vec3 d3 = d0 - (1.0 - 3.0 * K2);

  vec4 h = max(0.6 - vec4(dot(d0, d0), dot(d1, d1), dot(d2, d2), dot(d3, d3)), 0.0);
  vec4 n = h * h * h * h *
           vec4(dot(d0, hash33(i)), dot(d1, hash33(i + i1)), dot(d2, hash33(i + i2)), dot(d3, hash33(i + 1.0)));

  return dot(vec4(31.316), n);
}

vec4 encodeUnitFloat32(float f) {
  vec4 rgba = vec4(1., 255., 65025., 16581375.) * f;
  rgba = fract(rgba);
  rgba -= rgba.yzww * vec2(1. / 255., 0.).xxxy;
  return rgba;
}

void main(void) {
  vec2 uv = vUV * 12. + vec2(10.);

  float b = clamp(noise(vec3(uv, iTime * .5)) * .25 + .35, 0., 1.);

  gl_FragColor = encodeUnitFloat32(b);
}
